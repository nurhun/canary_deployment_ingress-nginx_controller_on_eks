##################### 1-vpc #####################

data "aws_availability_zones" "available" {}

locals {
  vpc_cidr = "10.0.0.0/16"
  azs      = slice(data.aws_availability_zones.available.names, 0, 3)
}

module "vpc" {
  source = "terraform-aws-modules/vpc/aws"

  name = "eks-vpc"
  cidr = local.vpc_cidr

  azs             = local.azs
  private_subnets = [for k, v in local.azs : cidrsubnet(local.vpc_cidr, 8, k)]
  public_subnets  = [for k, v in local.azs : cidrsubnet(local.vpc_cidr, 8, k + 4)]

  # The load balancer controller uses these tags to discover subnets in which it can create load balancers. 
  public_subnet_tags = {
    "kubernetes.io/role/elb" = "1"
  }
  private_subnet_tags = {
    "kubernetes.io/role/internal-elb" = "1"
  }

  enable_nat_gateway     = true
  single_nat_gateway     = true
  one_nat_gateway_per_az = false

  # Determines whether the VPC supports assigning public DNS hostnames to instances with public IP addresses.
  enable_dns_hostnames = true
  # Determines whether the VPC supports DNS resolution through the Amazon provided DNS server.
  enable_dns_support = true

  tags = {
    Environment = "devops-test"
  }
}